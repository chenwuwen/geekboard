//模块级构建文件 —— build.gradle
//模块级 build.gradle 文件位于每个 <project>/<module>/ 目录

apply plugin: 'com.android.application'
//apply plugin: 'com.jakewharton.butterknife'
//配置GraphQL 这一行应该在com.android.application下面。如果你想在你的Kotlin项目中使用Apollo，请将 Apollo 插件添加在 Kotlin 插件之前
//apply plugin: 'com.apollographql.android'
//应用GreenDao依赖
apply plugin: 'org.greenrobot.greendao'

//android {}块配置所有Android特定的构建选项
android {
//    添加签名文件配置
    signingConfigs {
        release {
            keyAlias 'geekboard'
            keyPassword '123456'
            storeFile file('geekboard.jks')
            storePassword '123456'
        }
    }

    /**
     * compileSdkVersion指定Gradle应该使用的Android API级别
     * 编译您的应用程序。 这意味着您的应用可以使用的API等于
     * 或低于这个API。
     *
     * buildToolsVersion指定SDK构建工具的版本，命令行
     * 以及Gradle用来构建应用程序的编译器。 你需要
     * 使用SDK管理器下载构建工具。
     */
//    引入变量 如果引入的变量是记录在xxx.properties文件中的变量,那么引用到的变量默认是String类型，如果需要in类型，需要在后面添加 as int 声明
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    /**
     * defaultConfig {}块封装了所有构建variants的默认设置，
     * 并可以覆盖AndroidManifest.xml中的某些属性例如包名、
     * minSdkVersion 、versionCode扥
     */
    defaultConfig {

        flavorDimensions "default"
//        applicationId唯一，等同于AndroidManifest.xml文件中的package属性。
        applicationId "cn.kanyun.geekboard"
        // 最小API版本
        minSdkVersion 26
        // 项目测试运行的版本
        targetSdkVersion 27
        // 版本号 (VersionCode：对消费者不可见，仅用于应用市场、程序内部识别版本，判断新旧等用途) https://blog.csdn.net/attheway/article/details/24255793
        versionCode 14
        // 在app中显示的版本名称(VersionName：展示给消费者，消费者会通过它认知自己安装的版本)
        versionName "1.0.0"

//        当使用Lombok时需要再次声明该配置(应当是当使用一些插件此插件会在编译期起作用时,声明该配置)
        javaCompileOptions { annotationProcessorOptions { includeCompileClasspath = true } }


//        安卓真机或者模拟器运行安装应用时提示 Failure [INSTALL_FAILED_NO_MATCHING_ABIS: Failed to extract native libraries, res=-113]
//        这个错误提示, 是由于使用了native libraries 。该native libraries 不支持当前的cpu的体系结构。
//        解决办法： 现在安卓模拟器的 CPU/ABI 一般有三种类型：INTEL X86；ARM；MIPS .既然用 INTEL X86 出现 INSTALL_FAILED_NO_MATCHING_ABIS 的错误，那就改用 ARM 的吧。
        splits {
            abi {
                enable true
                reset()
                include 'x86', 'armeabi-v7a','x86_64'
                universalApk true
            }
        }
    }

//    默认生成的release版apk名为app-release.apk
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                // 此处指定生成的apk文件名
                outputFileName = "geekboard.apk"
            }
    }

    /**
     * buildTypes {}块可以配置多个构建类型。
     * 默认情况下，构建系统定义了两种构建类型：debug和release。
     */
    buildTypes {

        /**
         * 默认情况下，Android Studio会配置发布版本类型以启用缩减
         * 代码，使用minifyEnabled，并指定Proguard设置文件。
         */
        release {
            // 启用缩减代码
            minifyEnabled true
//           使用代码混淆
            useProguard true
//            代码混淆使用哪个配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            是否启用未使用资源的收缩。默认值为false
            shrinkResources true
//            签名配置
            signingConfig signingConfigs.release
        }
//        debug下打出的包会更大
        debug {
            signingConfig signingConfigs.release
//            是否应该生成可调试的apk
            debuggable true
        }
    }

    /**
     * 多渠道打包
     * productFlavors {}块是您可以配置不同定制的产品，例如免费和付费
     * 这样的话,打包时会同时打几种不同渠道的包
     */
    productFlavors {
//        free {
//            applicationId 'cn.kanyun.geekboard.free'
//        }
//
//        paid {
//            applicationId 'cn.kanyun.geekboard.paid'
//        }
    }

    /**
     * split {}块中配置不同的APK版本，用于分包
     */
    splits {
        // 屏幕密度分割设置
        density {
            // 启用或禁用密度分割机制
            enable false
            // 不包含以下密度
            exclude "ldpi", "tvdpi", "xxxhdpi", "400dpi", "560dpi"
        }
    }

//    编译设置
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    //排除掉中间生成的DEPENDENCIES.txt文件
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/rxjava.properties'
    }

//    默认情况下src/mian/res/xx 下不能添加子文件夹(如果添加了子文件夹那么R.id将找不到对应资源),那么就会导致布局/图片/样式文件都堆在一块,通过设置
//    sourceSets 设置来解决这个问题，同时需要注意该代码块的位置,其应在 android{}块内
//    https://www.jianshu.com/p/787a90c9b042
    sourceSets {
        main {
            java.srcDirs = [
                    'src/main/java'
            ]
            res.srcDirs = [
//                    本想利用gradle的sourceSets来实现res下创建子文件夹用来分类资源,发现并没有什么用
//                    'src/main/res/drawable/donation',
//                    'src/main/res/drawable/foot_tab',
//                    'src/main/res/drawable/skin_preview',
//                    'src/main/res/drawable',
'src/main/res'
            ]
            assets.srcDirs = ['src/main/assets', 'src/main/assets/']
        }
    }

//    lint检查对于app来说也是很重要的，可以检查出很多问题
    lintOptions {
        checkReleaseBuilds false

        // 关闭指定问题检查
        disable 'InvalidPackage'

        // true--生成txt格式报告(默认false)
        textReport true
        // 重定向输出；可以是文件或'stdout'
        textOutput 'stdout'
        // true--错误发生后停止gradle构建
        abortOnError false
        // true--生成XML格式报告
        xmlReport false
        // 指定xml报告文档(默认lint-results.xml)
        xmlOutput file("lint-report.xml")
        // true--生成HTML报告(带问题解释，源码位置，等)
        htmlReport true
        // html报告可选路径(构建器默认是lint-results.html )
        htmlOutput file("build/lint-report.html")
        //  true--所有正式版构建执行规则生成崩溃的lint检查，如果有崩溃问题将停止构建
        checkReleaseBuilds true
    }

    dexOptions {
        //最大堆内存
        javaMaxHeapSize "8g"
        //是否支持大工程模式
        jumboMode = true
        //预编译
        preDexLibraries = true
        //线程数
        threadCount = 8
    }

    greendao{

        //指定数据库schema版本号，迁移等操作会用到;
        schemaVersion 1
        //dao的包名，包名默认是entity所在的包；
        daoPackage 'cn.kanyun.geekboard.gen'
        //生成数据库文件的目录;
        targetGenDir 'src/main/java'
    }
}

/**
 * dependencies {}块指定模块需要添加的依赖
 */
dependencies {
    api 'androidx.appcompat:appcompat:1.0.0'
    api 'androidx.legacy:legacy-support-v13:1.0.0'
//    AppIntro是一个APP启动页开源库
    implementation 'com.github.paolorotolo:appintro:v5.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
//    FloatingActionButton简称FAB 悬浮按钮
    implementation 'com.getbase:floatingactionbutton:1.10.1'
//    ButterKnife是一个专注于Android系统的View注入框架,以前总是要写很多findViewById来找到View对象，有了ButterKnife可以很轻松的省去这些步骤。目前使用很广。最重要的一点，使用ButterKnife对性能基本没有损失，因为ButterKnife用到的注解并不是在运行时反射的，而是在编译的时候生成新的class。项目集成起来也是特别方便，使用起来也是特别简单
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.github.louisgeek:LouisJavaMail:1.0.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.9'
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation group: 'com.google.guava', name: 'guava', version: '28.0-android'

//    implementation group: 'javax.mail', name: 'javax.mail-api', version: '1.6.2'
//    邮件依赖,不能使用上面的依赖,会导致冲突Duplicate class javax.mail.Address found in modules
//    https://blog.csdn.net/richiezhu/article/details/79578483
    implementation 'com.sun.mail:android-mail:1.6.2'
    implementation 'com.sun.mail:android-activation:1.6.2'

//    RxAndroid主要的作用也是异步，随意定制主线程、子线程的操作，链式编程，让我们的代码可读性大大增强
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.1.1'
//    RxJava 主要的作用就是链式完成异步操作,并且非常强大，RxJava最核心的两个东西是Observables（被观察者，事件源）和Subscribers（订阅者）。Observables发出一系列事件，Subscribers处理这些事件
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.10'

//    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.0.0'
//    必须使用 （OkGo - 3.0 一个基于okhttp的标准RESTful风格的网络框架） https://github.com/jeasonlzy/okhttp-OkGo/wiki/OkGo
    implementation 'com.lzy.net:okgo:3.0.4'
//    以下三个选择添加，okrx和okrx2不能同时使用
//    implementation 'com.lzy.net:okrx:1.0.2'
    implementation 'com.lzy.net:okrx2:2.0.2'
    implementation 'com.lzy.net:okserver:2.0.5'

//    android support design包已经转移到这个下面了
    implementation 'com.google.android.material:material:1.1.0-alpha07'

//    强大的Android工具类：https://github.com/Blankj/AndroidUtilCode  http://www.apkbus.com/thread-593099-1-4.html
    implementation 'com.blankj:utilcodex:1.24.7'


    //BasePopup主体库(弹出层,非dialog)
    implementation 'com.github.razerdp:BasePopup:2.2.1'
    //以下可选
    //BasePopup support支持库（如支持DialogFragment里弹Popup时的层序支持）
//    implementation 'com.github.razerdp:BasePopup-compat-support:2.2.1'
    //BasePopup lifecycle支持库（如自动适配Activity/Fragment生命期dismiss/回收等）
    implementation 'com.github.razerdp:BasePopup-compat-lifecycle:2.2.1'
    //BasePopup androidx支持库（针对以上两个支持的androidX版本）
    implementation 'com.github.razerdp:BasePopup-compat-androidx:2.2.1'

//    Glide 是 Google 员工的开源项目， Google I/O 上被推荐使用，一个高效、开源、Android设备上的媒体管理框架
//    https://muyangmin.github.io/glide-docs-cn/doc
    implementation group: 'com.github.bumptech.glide', name: 'glide', version: '4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

//    使用微信分享、登录、收藏、支付等功能需要的库以及文件（包含统计功能）
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:+'

//    AutoLayoutActivity是专为安卓手机屏幕适配而设计的开源项目，AutoLayoutActivity根据开发者在View布局时设置宽高的值，再根据手机屏幕分辨率的不同，而进行等比例的分配。也就类似于我们经常用到的适配方法在LinearLayout中设置的权重比例。在开发过程中，总有一些地方是无法用match_parent和wrap_content进行分配，或者相对布局来解决。所以引入了AutoLayout，这样便可以随意设置px或者dp，而不会造成在不同手机屏幕分辨率上的不同显示效果，甚至变形了
//    https://www.jianshu.com/p/65a7b1bc136e
    implementation 'com.zhy:autolayout:1.4.5'

//    Android如果需要使用Lombok,除了Android Studio需要安装Lombok插件外,build.gradle还要添加以下两个依赖
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.projectlombok:lombok:1.18.8'

//    ImageLoad 用于加载图片
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

//    这是一个开源的dialog组件(这个组件的2.x/3.x版本只适用于Kotlin)
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

//    implementation 'com.apollographql.apollo:apollo-runtime:1.0.2-SNAPSHOT'

    compileOnly 'org.jetbrains:annotations:13.0'
    testCompileOnly 'org.jetbrains:annotations:13.0'

//    SwitchButton开关按钮  https://github.com/kyleduo/SwitchButton
    implementation 'com.kyleduo.switchbutton:library:2.0.0'

//    动态权限库(同类还有RxPermissions)  https://github.com/googlesamples/easypermissions
    implementation 'pub.devrel:easypermissions:2.0.1'

//    好用的按钮组件：https://github.com/ansnail/SuperButton
    implementation 'top.androidman:superbutton:1.0.1'

//    orm框架
    implementation 'org.greenrobot:greendao:3.2.2'
//    日志框架：功能包括 a、打印线程的信息 b、打印类的信息 c、打印方法的信息 d、可以打印list、map、json、xml e、不必每次输入TAG f、信息过长会自动换行 g、从日志直接跳转到源码
    implementation 'com.orhanobut:logger:2.2.0'

//    图片裁剪库 https://github.com/Yalantis/uCrop
    implementation 'com.github.yalantis:ucrop:2.2.3'



}


